apply plugin: 'cf-app'

bootJar {
    enabled = true
    archiveClassifier = 'exec'
}

jar.enabled = false

bootRun {
    systemProperties = System.properties
}

cfConfig {
    //CF Details
    ccHost = "api.run.pivotal.io"
    ccUser = "admin"
    ccPassword = "admin"
    org = "myOrg"
    space = "mySpace"

    //App Details
    name = "${bootJar.archiveBaseName.get()}"
    host = "${bootJar.archiveBaseName.get()}"
    filePath = "${layout.buildDirectory.get()}/libs/${bootJar.archiveFileName.get()}"
    path = ""
    domain = "cfapps.io"
    instances = 1
    memory = 1024

    //Env and services
    buildpack = "https://github.com/cloudfoundry/java-buildpack"
    environment = ["JAVA_OPTS": "-Djava.security.egd=file:/dev/./urandom", "SPRING_PROFILES_ACTIVE": "cloud"]
}

tasks.named("bootBuildImage") {
    imageName = "${dockerImagePrefix}/${bootJar.archiveBaseName.get()}:${project.version}"
    tags = ["${dockerImagePrefix}/${bootJar.archiveBaseName.get()}:latest"]
    environment = [
        "BP_JVM_VERSION": "21"
    ]
    if (project.hasProperty("publishImage") && publishImage.toBoolean()) {
        publish = true
        docker {
            publishRegistry {
                url = "us.gcr.io"
                username = "oauth2accesstoken"
                password = file('build/oauth2accesstoken').text.trim()
            }
        }
    }
}

dependencies {
    implementation project(':cities-domain')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation('org.springframework.data:spring-data-rest-hal-explorer')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation('com.zaxxer:HikariCP')
    implementation('io.micrometer:micrometer-registry-prometheus:1.15.4')
    runtimeOnly('org.hsqldb:hsqldb:2.7.4')
    runtimeOnly('org.postgresql:postgresql:42.7.8')
}

